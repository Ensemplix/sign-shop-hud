buildscript {
    repositories {
        mavenCentral()

        maven {
            url = "http://files.minecraftforge.net/maven"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

group = 'ru.ensemplix.shop'
version = '1.0.1'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()

    maven {
        url 'http://maven.ensemplix.ru/artifactory/Ensemplix/'
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade 'ru.ensemplix.shop:sign-shop-data-importer:1.0-SNAPSHOT'
    shade 'ru.ensemplix.shop:sign-shop-price-parser:1.0-SNAPSHOT'
    shade 'ru.ensemplix.shop:sign-shop-data-core:1.0-SNAPSHOT'

    testCompile 'org.mockito:mockito-core:2.3.0'
    testCompile 'junit:junit:4.12'
}

jar {
    baseName = 'SignShopHud'

    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

minecraft {
    version = '1.9-12.16.1.1907'
    mappings = 'stable_24'
    runDir = 'run'
}

task setup {
    dependsOn 'setupDecompWorkspace'
    dependsOn 'setupDevWorkspace'
    dependsOn 'idea'
    tasks.findByName('setupDevWorkspace').mustRunAfter 'setupDecompWorkspace'
    tasks.findByName('idea').mustRunAfter 'setupDevWorkspace'
}
